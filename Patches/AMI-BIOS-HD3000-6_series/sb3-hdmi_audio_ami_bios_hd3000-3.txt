# Maintained by: toleda for: github.com/toleda_hdmi_hd3000
# File: sb3-hdmi_audio_ami_bios_hd3000-3.txt_v1.1
# Adds Desktop AMD/Nvidia/HD3000 HDMI audio dsdt edits  (Audio ID: 1)
#
# 1. Remove PEGP device - if present
# 2. Rename GFX0 devices to IGPU - Verify IOReg/GFXO@2
# 3. Rename device P0P1 to device PEGP - Verify IOReg/P0P1@1
# 4. Add method DTGP to the main block - if not present
# 5. Insert device GFX0 and device HDAU into device PEGP (Nvidia/AMD HDMI audio)
# 6. Insert HDMI audio injection into device IGPU (HD3000 HDMI audio - Part 1/2)
# 7. Add Device HDEF and HDMI audio injection (HD3000 HDMI audio - Part 2/2)
#
# 1. Remove PEGP device
into device label PEGP remove_entry;
into scope label _SB.PCI0.PEG0.PEGP remove_entry;
into_all all code_regex Notify\s\(\\_SB.PCI0.PEG0.PEGP,\s0x.*\) removeall_matched;
# 2. Rename GFX0 devices to IGPU
into device label GFX0 set_label begin IGPU end;
into_all all code_regex GFX0 replaceall_matched begin IGPU end;
into scope label _SB.PCI0.GFX0 set_label begin _SB.PCI0.IGPU end;
into scope label \_SB.PCI0.GFX0 set_label begin \_SB.PCI0.IGPU end;
# 3. Rename device P0P1 to device PEGP
into device label P0P1 set_label begin PEGP end;
into_all all code_regex P0P1 replaceall_matched begin PEGP end;
into scope label _SB.PCI0.P0P1 set_label begin _SB.PCI0.PEGP end;
into scope label \_SB.PCI0.P0P1 set_label begin \_SB.PCI0.PEGP end;
# 4. Add method DTGP to the main block, it's used by other patches
into method label DTGP remove_entry;
into definitionblock code_regex . insert
begin
Method (DTGP, 5, NotSerialized)\n
{\n
    If (LEqual (Arg0, Buffer (0x10)\n
            {\n
                /* 0000 */    0xC6, 0xB7, 0xB5, 0xA0, 0x18, 0x13, 0x1C, 0x44, \n
                /* 0008 */    0xB0, 0xC9, 0xFE, 0x69, 0x5E, 0xAF, 0x94, 0x9B\n
            }))\n
    {\n
        If (LEqual (Arg1, One))\n
        {\n
            If (LEqual (Arg2, Zero))\n
            {\n
                Store (Buffer (One)\n
                    {\n
                        0x03\n
                    }, Arg4)\n
                Return (One)\n
            }\n
            If (LEqual (Arg2, One))\n
            {\n
                Return (One)\n
            }\n
        }\n
    }\n
    Store (Buffer (One)\n
        {\n
            0x00\n
        }, Arg4)\n
    Return (Zero)\n
}
end;
# 5.  Insert device GFX0 and device HDAU (Nvidia/AMD HDMI audio)
into method label _DSM parent_adr 0x00010000 remove_entry;
into device name_adr 0x00010000 insert
begin
Device (GFX0)\n
{\n
    Name (_ADR, Zero)\n
    Name (_SUN, One)\n
    Method (_DSM, 4, NotSerialized)\n
    {\n
        Store (Package (0x0a)\n
            {\n
                "@0,connector-type", \n
                Buffer (0x04)\n
                {\n
                    0x00, 0x08, 0x00, 0x00\n
                }, \n
                "@1,connector-type", \n
                Buffer (0x04)\n
                {\n
                    0x00, 0x08, 0x00, 0x00\n
                }, \n
                "@2,connector-type", \n
                Buffer (0x04)\n
                {\n
                    0x00, 0x08, 0x00, 0x00\n
                }, \n
                "@3,connector-type", \n
                Buffer (0x04)\n
                {\n
                    0x00, 0x08, 0x00, 0x00\n
                }, \n
                "hda-gfx", \n
                Buffer (0x0A)\n
                {\n
                     "onboard-2" \n
                }, \n
            }, Local0)\n
        DTGP (Arg0, Arg1, Arg2, Arg3, RefOf (Local0))\n
        Return (Local0)\n
    }\n
  }\n
Device (HDAU)\n
{\n
    Name (_ADR, One)\n
    Method (_DSM, 4, NotSerialized)\n
    {\n
        Store (Package (0x02)\n
            {\n
                "hda-gfx", \n
                Buffer (0x0A)\n
                {\n
                     "onboard-2" \n
                }, \n
            }, Local0)\n
        DTGP (Arg0, Arg1, Arg2, Arg3, RefOf (Local0))\n
        Return (Local0)\n
    }\n
  }\n
end;
# 6. Insert HDMI audio injection into device IGPU (HD3000 HDMI audio - Part 1/2)
into scope label _DSM parent_adr 0x00020000 remove_entry;
into scope name_adr 0x00020000 insert
begin
    Method (_DSM, 4, NotSerialized)\n
    {\n
        Store (Package (0x06)\n
 	     {\n
                "device-id", \n
                Buffer (0x04) \n
                { \n
                   0x26, 0x01, 0x00, 0x00 \n
                }, \n
                "AAPL,snb-platform-id", \n
                Buffer (0x04) \n
                { \n
                   0x10, 0x00, 0x03, 0x00 \n
                }, \n
                "hda-gfx", \n
                Buffer (0x0A)\n
                {\n
                     "onboard-1" \n
                } \n
            }, Local0)\n
        DTGP (Arg0, Arg1, Arg2, Arg3, RefOf (Local0))\n
        Return (Local0)\n
    }\n
end;
# 7. Add Device HDEF and HDMI audio injection (HD3000 HDMI audio - Part 2/2)
into device name_adr 0x001B0000 parent_label PCI0 remove_entry;
into device label PCI0 insert
begin
Device (HDEF) \n
{ \n
   Name (_ADR, 0x001B0000) \n 
   Name (_PRW, Package (0x02) \n
   { \n
        0x09, \n
        0x05 \n
    }) \n
    Method (_DSM, 4, NotSerialized)\n
    {\n
        Store (Package (0x06)\n
 	     {\n
                "hda-gfx", \n
                Buffer (0x0A)\n
                {\n
                     "onboard-1" \n
                }, \n
                "layout-id", \n
                Buffer (0x04)\n
                {\n
                    0x03, 0x00, 0x00, 0x00\n
                }, \n
                "PinConfigurations", \n
                Buffer (Zero) {}\n
            }, Local0)\n
        DTGP (Arg0, Arg1, Arg2, Arg3, RefOf (Local0))\n
        Return (Local0)\n
    }\n
}\n
end;
into definitionblock code_regex . insert
begin Store ("sb3-hdmi_audio_ami_bios_hd3000-3.txt_v1.1 dsdt edits, github.com/toleda", Debug) end
